TEMPLATES := "$CIT_BASE" + "/resources/templates"

help:
  @just --list

# Scaffold a python package
python NAME DESCRIPTION='package_description':
  cp -r {{TEMPLATES}}/python-pkg {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' pyproject.toml && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' README.md && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' src/NAME/__init__.py && \
  mv src/NAME src/{{snakecase(file_name(NAME))}}

# Scaffold a typescript node package
typescript NAME DESCRIPTION='package_description':
  cp -r {{TEMPLATES}}/ts-pkg {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' package.json && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' README.md && \
  yarn

# Scaffold a typescript node package
node18-typescript NAME DESCRIPTION='package_description':
  cp -r {{TEMPLATES}}/node18-ts {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' package.json && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' README.md && \
  yarn

# Scaffold a react x vite app
react NAME='app' DESCRIPTION='app_description':
  cp -r {{TEMPLATES}}/react-vite {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' package.json && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g; s/<DESCRIPTION>/{{DESCRIPTION}}/g' index.html && \
  yarn

# Scaffold a react x vite playground app
playground NAME='playground':
  cp -r {{TEMPLATES}}/react-vite-playground {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g' package.json && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g' index.html && \
  yarn

# Scaffold a react x vite x capacitor app. The app ID is set to "com.moveread.{{NAME}}"
capacitor NAME APP_NAME:
  cp -r {{TEMPLATES}}/react-capacitor-vite {{invocation_directory()}}/{{NAME}}
  cd {{invocation_directory()}}/{{NAME}} && \
  sed -i 's/<NAME>/{{file_name(NAME)}}/g' package.json && \
  sed -i 's/<NAME>/{{titlecase(file_name(NAME))}}/g' index.html && \
  echo 'APP_ID="com.moveread.{{NAME}}"' > .env && \
  echo 'APP_NAME="{{APP_NAME}}"' >> .env && \
  echo "PC_LAN_IP=\"`$FIND_LAN_IP`\"" >> .env && \
  yarn && \
  just android setup